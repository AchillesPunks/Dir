apply plugin: 'com.android.application'

project.archivesBaseName = "dir"

android {
    compileSdkVersion 23
    buildToolsVersion '26.0.2'

    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(file('version.properties')))

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 23
        versionName versionProps['name']
        versionCode versionProps['code'].toInteger()

        testApplicationId "com.veniosg.dir.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    signingConfigs {
        release {
            def Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('../local.properties')))
            def Properties keyProps = new Properties()
            keyProps.load(new FileInputStream(file('../keystore.properties')))
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-rules.txt')
            signingConfig signingConfigs.release
        }

        publicBeta.initWith(buildTypes.release)
        publicBeta {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-rules.txt')
            versionNameSuffix " " + versionProps['betaNumber']
        }

        publicDebug.initWith(buildTypes.publicBeta)
        publicDebug {
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-rules.txt')
            versionNameSuffix " Debug " + versionProps['betaNumber']
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    annotationProcessor "android.arch.lifecycle:compiler:1.0.0-alpha9-1"
    annotationProcessor "com.google.dagger:dagger-compiler:2.4"

//    implementation "com.android.support:appcompat-v7:23.4.0"
    implementation 'com.android.support:recyclerview-v7:23.4.0'
    implementation "android.arch.lifecycle:runtime:1.0.0"
    implementation "android.arch.lifecycle:extensions:1.0.0-alpha9-1"
    implementation "com.google.dagger:dagger:2.4"
    implementation "io.reactivex.rxjava2:rxjava:2.1.1"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
    implementation "com.nostra13.universalimageloader:universal-image-loader:1.9.5"

    androidTestImplementation "com.jayway.android.robotium:robotium-solo:5.0.1"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.10.0'
    testImplementation "android.arch.core:core-testing:1.0.0-alpha9-1"
}
